set(CMAKE_CXX_EXTENSIONS OFF)

macro(enable_strict_compiler_checks)
    if(MSVC)
        add_compile_definitions(__STDC_WANT_SECURE_LIB__)
        add_compile_options(/sdl /W4 /WX)
        add_link_options(/WX)
    else()
        add_compile_options(-Wall -Wextra -Werror -pedantic -pedantic-errors)
    endif()
endmacro()

if(ASAN)
    message("-- Address sanitizing: enabled")
    if(MSVC)
        add_compile_options(/fsanitize=address)
        add_link_options(/IGNORE:4300)
    else()
        add_compile_options(-fsanitize=address)
        add_link_options(-fsanitize=address)
    endif()
else()
    message("-- Address sanitizing: disabled (use -D ASAN=ON to enable)")
endif()

function(ctest_sanitize_executable TARGET_NAME)
    if(ASAN)
        add_test(NAME "${TARGET_NAME}" COMMAND "${TARGET_NAME}")
        if(NOT MSVC AND NOT CMAKE_CXX_COMPILER_ID MATCHES "AppleClang")
            set_property(TEST "${TARGET_NAME}" PROPERTY ENVIRONMENT ASAN_OPTIONS=detect_leaks=1)
        endif()
    endif()
endfunction()
